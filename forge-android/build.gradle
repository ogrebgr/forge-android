buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'



ext {
    forge_android_version = '9.2.6'
}
group = 'com.bolyartech.forge.android'
version = forge_android_version


//apply from: 'bintray-config.gradle'

publish {
    userOrg = 'ogibankov'
    groupId = 'com.bolyartech.forge.android'
    artifactId = 'forge-android'
    publishVersion = forge_android_version
    desc = 'Forge framework for Andoid'
    website = 'https://github.com/ogrebgr/forge-android'
}


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}
artifacts {
//    archives javadocJar
    archives sourcesJar
}


dependencies {
    api project(':forge-base')
//    api 'com.bolyartech.forge:base:7.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation 'javax.inject:javax.inject:1'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'javax.annotation:jsr250-api:1.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'javax.annotation', module: 'javax.annotation-api'
        exclude group: 'com.google.code.findbugs'
    }
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
}

tasks.withType(Javadoc).all {
    enabled = false
}