apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    forge_android_version = '8.0.4'
}
group = 'com.bolyartech.forge.android'
version = forge_android_version


publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.bolyartech.forge.android'
            artifactId 'forge-android'
            version forge_android_version
            artifact("$buildDir/outputs/aar/forge-android-release.aar")
            artifact sourcesJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

apply from: 'bintray-config.gradle'

bintray {
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'forge-android'
        userOrg = 'ogibankov'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/ogrebgr/forge-android'
        issueTrackerUrl = 'https://github.com/ogrebgr/forge-android/issues'
        vcsUrl = 'https://github.com/ogrebgr/forge-android.git'
        version {
            name = forge_android_version
            desc = 'Forge android framework'
            vcsTag = 'v' + forge_android_version
        }
    }
    publications = ['MyPublication']
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}
artifacts {
//    archives javadocJar
    archives sourcesJar
}


dependencies {
//    implementation project(':forge-base')
    implementation 'com.bolyartech.forge:base:6.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation 'javax.inject:javax.inject:1'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'javax.annotation:jsr250-api:1.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'javax.annotation', module: 'javax.annotation-api'
        exclude group: 'com.google.code.findbugs'
    }
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
}
